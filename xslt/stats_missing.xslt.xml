<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="#default">
  <xsl:include href="converters.xslt.xml"/>
  <xsl:include href="footer.xslt.xml"/>
  <xsl:include href="page.xslt.xml"/>
  <xsl:include href="stats_css.xslt.xml"/>
  <xsl:include href="sums_row_table.xslt.xml"/>
  <xsl:param name="page_type"/>
  <xsl:param name="date_pretty"/>
  <xsl:param name="prevpath"/>
  <xsl:variable name="title">
    <xsl:choose>
      <xsl:when test="count(/series/papers)=1">
	<xsl:text>The record&#32;</xsl:text>
      </xsl:when>
      <xsl:otherwise>
	<xsl:text>The&#32;</xsl:text>
	<xsl:value-of select="count(/series/papers)"/>
	<xsl:text>&#32;records&#32;</xsl:text>
      </xsl:otherwise>
    </xsl:choose>
    <xsl:text>&#32;in series&#32;</xsl:text>
    <a>
      <xsl:attribute name="href">
	<xsl:value-of select="$collection_linker"/>
	<xsl:value-of select="/series/@handle"/>
      </xsl:attribute>
      <xsl:value-of select="$series_handle_repec"/>
    </a>
    <!-- ## page type '_' is for all records -->
    <xsl:if test="not($page_type='_')">
      <xsl:text>&#32;for which </xsl:text>
      <xsl:value-of select="$project"/>
      <xsl:text>&#32;does not have&#32;</xsl:text>
      <xsl:choose>
	<xsl:when test="$page_type='has_futli'">
	  <xsl:text>any full-text links</xsl:text>
	</xsl:when>
	<xsl:when test="$page_type='warcs'">
	  <xsl:text>&#32;a web archive</xsl:text>
	</xsl:when>
	<xsl:when test="$page_type='pdf_warcs'">
	  <xsl:text>&#32;a PDF file in the web archive</xsl:text>
	</xsl:when>
	<xsl:when test="$page_type='json'">
	  <xsl:text>&#32;a PDF file coverted to JSON</xsl:text>
	</xsl:when>
	<xsl:otherwise>
	  <xsl:comment>
	    <xsl:text>ERROR: I can't handle the case </xsl:text>
	    <xsl:value-of select="$page_type"/>
	  </xsl:comment>
	</xsl:otherwise>
      </xsl:choose>
    </xsl:if>
    <xsl:text>.</xsl:text>
  </xsl:variable>
  <xsl:variable name="series_handle_repec">
    <xsl:call-template name="repec">
      <xsl:with-param name="in"
                      select="/series/@handle"/>
    </xsl:call-template>
  </xsl:variable>
  <xsl:template name="results">
    <h1>
      <xsl:copy-of select="$title"/>
    </h1>
    <ol>
      <xsl:for-each select="/series/papers">
	<li>
	  <a>
	    <xsl:attribute name="href">
              <xsl:value-of select="$linker"/>
              <xsl:value-of select="@handle"/>
	    </xsl:attribute>
	    <xsl:call-template name="repec">
              <xsl:with-param name="in"
                              select="@handle"/>
	    </xsl:call-template>
	  </a>
	</li>
	<xsl:text>&#10;</xsl:text>
      </xsl:for-each>
    </ol>
    <xsl:call-template name="footer"/>
  </xsl:template>
  <xsl:template match="/">
    <xsl:call-template name="page">
      <xsl:with-param name="title"
		      select="$title"/>
      <xsl:with-param name="body">
	<xsl:call-template name="results"/>
      </xsl:with-param>
      <xsl:with-param name="css_data">
	<xsl:copy-of select="$css"/>
      </xsl:with-param>
    </xsl:call-template>
  </xsl:template>
</xsl:stylesheet>
