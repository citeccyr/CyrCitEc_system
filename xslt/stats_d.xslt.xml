<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="#default">
  <xsl:include href="converters.xslt.xml"/>
  <xsl:include href="page.xslt.xml"/>
  <xsl:include href="footer.xslt.xml"/>
  <xsl:include href="pdf.xslt.xml"/>
  <xsl:include href="stats_css.xslt.xml"/>
  <xsl:include href="sums_row_table.xslt.xml"/>
  <xsl:param name="date_pretty"/>
  <xsl:param name="prevpath"/>
  <xsl:variable name="prefix"
                select="'spz:ranepa:refs:'"/>
  <xsl:variable name="papid"
		select="normalize-space(/document/source/@handle)"/>
  <xsl:variable name="count"
		select="count(//reference[@*[name()='handle' or name()='doi']])"/>
  <xsl:template name="title">
    <xsl:text>The&#32;</xsl:text>
    <xsl:call-template name="english_number">
      <xsl:with-param name="in"
		      select="$count"/>
    </xsl:call-template>
    <xsl:text>&#32;</xsl:text>
    <xsl:comment>
      <xsl:value-of select="$count"/>
    </xsl:comment>
    <xsl:text>&#32;linked reference</xsl:text>
    <xsl:if test="$count &gt; 1">
      <xsl:text>s</xsl:text>
    </xsl:if>
    <xsl:text>&#32;in paper&#32;</xsl:text>
    <a>
      <xsl:attribute name="href">
	<xsl:value-of select="$linker"/>
	<xsl:value-of select="$papid"/>
      </xsl:attribute>
      <xsl:value-of select="$papid"/>
    </a>
  </xsl:template>
  <xsl:template name="body">
    <h1>
      <xsl:call-template name="title"/>
    </h1>
    <ol>
      <xsl:for-each select="//reference[@*[name()='handle' or name()='doi']]">
	<xsl:text>&#10;</xsl:text>
	<li>
	  <a>
	    <xsl:attribute name="href">
	      <xsl:choose>
		<xsl:when test="./@doi">
		  <xsl:value-of select="$doi_linker"/>
		  <xsl:value-of select="./@doi"/>
		</xsl:when>
		<xsl:when test="./@handle">
		  <xsl:value-of select="$linker"/>
		  <xsl:value-of select="./@handle"/>
		</xsl:when>
	      </xsl:choose>
	    </xsl:attribute>
	    <xsl:value-of select="./from_pdf"/>
	  </a>
	  <xsl:if test="./@doi and ./@handle">
	    <xsl:text>&#32;(</xsl:text>
	    <a>
	      <xsl:attribute name="href">
		<xsl:value-of select="$linker"/>
	      <xsl:value-of select="./@handle"/>
	      </xsl:attribute>
	      <xsl:text>the paper at Socionet</xsl:text>
	    </a>
	    <xsl:text>)</xsl:text>
	  </xsl:if>
	</li>
	<xsl:text>&#10;</xsl:text>
      </xsl:for-each>
    </ol>
    <xsl:text>&#10;</xsl:text>
    <xsl:call-template name="footer"/>
  </xsl:template>
  <xsl:template match="/">
    <xsl:call-template name="page">
      <xsl:with-param name="title">
	<xsl:call-template name="title"/>
      </xsl:with-param>
      <xsl:with-param name="body">
	<xsl:call-template name="body"/>
      </xsl:with-param>
      <xsl:with-param name="css_data">
	<xsl:copy-of select="$css"/>
      </xsl:with-param>
    </xsl:call-template>
  </xsl:template>
</xsl:stylesheet>
